[<div class="wiki-body gollum-markdown-content instapaper_body" id="wiki-body">
        <div class="markdown-body">
          <p>#Types
The types are the shape of the topics connections.</p>
<p>Each publisher and subscriber in our domain will comunicate using a specific type for transference.</p>
<p>This types may be defined dynamically and/or discovered by the different participants as far as they need them.</p>
<h3>
<a aria-hidden="true" class="anchor" href="#lifecycle" id="user-content-lifecycle"><svg aria-hidden="true" class="octicon octicon-link" height="16" version="1.1" viewBox="0 0 16 16" width="16"><path d="M4 9h1v1H4c-1.5 0-3-1.69-3-3.5S2.55 3 4 3h4c1.45 0 3 1.69 3 3.5 0 1.41-.91 2.72-2 3.25V8.59c.58-.45 1-1.27 1-2.09C10 5.22 8.98 4 8 4H4c-.98 0-2 1.22-2 2.5S3 9 4 9zm9-3h-1v1h1c1 0 2 1.22 2 2.5S13.98 12 13 12H9c-.98 0-2-1.22-2-2.5 0-.83.42-1.64 1-2.09V6.25c-1.09.53-2 1.84-2 3.25C6 11.31 7.55 13 9 13h4c1.45 0 3-1.69 3-3.5S14.5 6 13 6z" fill-rule="evenodd"></path></svg></a>Lifecycle</h3>
<blockquote>
<ol>
<li>A need of the domain where the DDS is used needs to transmit a certain kind of information</li>
<li>The participant that will publish this information defines the shape of this type by composing already defined types</li>
<li>The participant registries the defined type under a name in the domain.</li>
<li>Other participants may fetch the type from the handshake tier of the connection or query it by its name.</li>
<li>The publisher user will fill up a DTO object defined as the type definition demands</li>
<li>The publisher will push this DTO into the topic, where it will be marshalled with a marshaller constructed as the type definition demands</li>
<li>The subscriber process will receive the marshaled data and unmarshall it into a DTO with a marshaller constructed as the type definition demands</li>
<li>The subscriber will execute a defined callback with  a DTO object defined as the type definition demands, equivalent to the sent one.</li>
</ol>
</blockquote>
<p>In what comes to this set of examples we are concerned about how to build a type definition, and from there, how to have a type, with it marshaller and DTO.</p>
<p>###1 -  Recognice a need</p>
<p>Lets picture we need to send the temperature of a big room. This room has several termometers in different positions of this room.
Then each measure of temperature should be related with a three dimensions position.
We should then define several types:
RoomTemperature = { TermometerMeasure1 .TermometerMeasure2 ... TermometerMeasureN }.
TermometerMeasure = { #measure -&gt; Float . #position -&gt; Position3D }.
Position3D = { #x -&gt;  Float . #y -&gt; Float . #z -&gt; Float }.
As usual, the complex types are defined in terms of other types.</p>
<p>###2-	Define the type
MetaDDS allows us to define types as following:</p>
<p>All together</p>
<pre><code>typeDefinition :=
MDDSTypeDefinition named: 'RoomTermometer' defined: { 
	MDDSFieldDefinition name: #stamp type: (MDDSBasicTypeDefinition for: MDDSTimeType new) .
	MDDSFieldDefinition name: #measures type: (
		MDDSCollectionTypeDefinition for: MDDSArrayType new storing: 
		(MDDSTypeDefinition named: 'TermometerMeasure' defined: { 
			MDDSFieldDefinition name: #measure type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
			MDDSFieldDefinition name: #position type: (MDDSTypeDefinition named: 'Position3D' defined: { 
				MDDSFieldDefinition name: #x type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
				MDDSFieldDefinition name: #y type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
				MDDSFieldDefinition name: #z type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
			})
		}))
}.
</code></pre>
<p>Also, for easing the lecture</p>
<pre><code>position3D := 
		MDDSTypeDefinition named: 'Position3D' defined: { 
				MDDSFieldDefinition name: #x type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
				MDDSFieldDefinition name: #y type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
				MDDSFieldDefinition name: #z type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
		}.	
termometerMeasure := 
		MDDSTypeDefinition named: 'TermometerMeasure' defined: { 
				MDDSFieldDefinition name: #measure type: (MDDSBasicTypeDefinition for: MDDSFloat32Type new) .
				MDDSFieldDefinition name: #position type: position3D
		}.
typeDefinition :=
		MDDSTypeDefinition named: 'RoomTermometer' defined: { 
				MDDSFieldDefinition name: #stamp type: (MDDSBasicTypeDefinition for: MDDSTimeType new) .
				MDDSFieldDefinition name: #measures type: (
				MDDSCollectionTypeDefinition for: MDDSArrayType new storing: termometerMeasure)
		}
</code></pre>
<p>As you can read in the code, each type definition is defined by field definitions. Each field definition is composed by a name and a type definition.
Besides the MDDSTypeDefinition class, used for define custom type definitions, we have also two others:
MDDSBasicTypeDefinition, used for basic types.
MDDSCollectionTypeDefinition, used for collections of types.</p>
<p>###3 - Registering a type definition</p>
<p>Since all what is to share is meta information, and since part of the type mechanism involves to generate needed classes, what we register in the domain is not the type it self, but the type definition. Then each system is able to read the definition and load it as it please.</p>
<p>For registering this information we just need to do</p>
<pre><code> domain := MDDSInternallyDefinedDomain new. " For different domains and how to build them, address the domain examples " 
domain registerTypeDefinition: typeDefinition.
</code></pre>
<p>After this line, you may be able to execute without any kind of fear to have an error the following line</p>
<pre><code>domain typeDefinition: 'RoomTemperature'.
</code></pre>
<p>If you still fearful you may want to execute</p>
<pre><code>domain typeDefinition: 'Position3D' ifAbsent: [  " Do something " ].
</code></pre>
<p>###4- Create type</p>
<p>There are two ways to create a type for a user. One is doing it by hand, and the other one with a type descriptor.</p>
<h4>
<a aria-hidden="true" class="anchor" href="#by-hand" id="user-content-by-hand"><svg aria-hidden="true" class="octicon octicon-link" height="16" version="1.1" viewBox="0 0 16 16" width="16"><path d="M4 9h1v1H4c-1.5 0-3-1.69-3-3.5S2.55 3 4 3h4c1.45 0 3 1.69 3 3.5 0 1.41-.91 2.72-2 3.25V8.59c.58-.45 1-1.27 1-2.09C10 5.22 8.98 4 8 4H4c-.98 0-2 1.22-2 2.5S3 9 4 9zm9-3h-1v1h1c1 0 2 1.22 2 2.5S13.98 12 13 12H9c-.98 0-2-1.22-2-2.5 0-.83.42-1.64 1-2.09V6.25c-1.09.53-2 1.84-2 3.25C6 11.31 7.55 13 9 13h4c1.45 0 3-1.69 3-3.5S14.5 6 13 6z" fill-rule="evenodd"></path></svg></a>By hand</h4>
<p>For creating a type we need basiclly three things:
&gt;- A domain where the type will live
&gt;- A type definition
&gt;- A type name</p>
<pre><code>domain := MDDSInternallyDefinedDomain new. " For different domains and how to build them, address the domain examples " 
type := domain createTypeNamed: 'RoomTemperature' definedBy: typeDefinition.
</code></pre>
<p>Once we create the type like this, there will be available a class called as each type created (Position3D, TermometerMeasure and RoomTemperature) and there will be a graph of objects for marshalling each of them.
Meanwhile the class is generated in case of need (If you have defined already the Position3D class by example, it will not replace it nor modificate it), the marshaller is created each time the domain es recreated.</p>
<p>The way to force the creation of the related classes and to obtain a marshaller is</p>
<pre><code>marshaller := typeDefinition typeBuilder buildTypeMarshaller.
</code></pre>
<p>####By type descriptor</p>
<pre><code>domain registerTypeDefinition: typeDefinition.

typeDescriptor := MDDSTypeDescriptor domain: domain name: 'TermometerMeasure'.
  type := typeDescriptor createType.
</code></pre>
<p>This version will try to fetch the type definition from the domain. If is not registered, it will fail.</p>
<p>After the types are collected by the garbage collector, the only things that remains is the generated DTO class.</p>

        </div>

    </div>]